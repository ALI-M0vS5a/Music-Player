plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

Properties getProp(File file) {
    def prop = new Properties()
    if (!file.exists()) {
        throw new IllegalArgumentException("couldn't find $file, make sure File is correct")
    }
    prop.load(new FileInputStream(file))
    return prop
}

Properties getRootProp(String propName) {
    def propFile = rootProject.file(propName)
    if (!propFile.exists()) {
        throw new IllegalStateException("couldn't find $propName in $rootProject")
    }
    def prop = getProp(propFile)
    return prop
}

Properties getLocalProp() {
    def prop = getRootProp("local.properties")
    return prop
}

Properties getKeystoreProp() {
    def prop = getRootProp("keystore.properties")
    return prop
}

android {

    compileSdk 32

    defaultConfig {
        applicationId "com.kylentt.musicplayer"
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xjvm-default=all"
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    sourceSets.all {
        kotlin.srcDir("src/$name/kotlin")
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    namespace 'com.kylentt.musicplayer'
}

dependencies {

    dependencies /* Android.material */ {
        def v = "1.6.1"
        implementation "com.google.android.material:material:$v"
    }

    dependencies /* Androidx.activity */ {
        def v = "1.5.0"
        implementation "androidx.activity:activity-compose:$v"
    }

    dependencies /* Androidx.compose */ {
        def v = "1.3.0-alpha01"

        // Core UI
        implementation "androidx.compose.ui:ui:$v"
        implementation "androidx.compose.ui:ui-tooling-preview:$v"

        // Foundation
        implementation "androidx.compose.foundation:foundation:$v"

        // Material
        implementation "androidx.compose.material:material:$v"
        implementation "androidx.compose.material:material-icons-core:$v"
        implementation "androidx.compose.material:material-icons-extended:$v"

        // Material3
        def vMaterial3 = "1.0.0-alpha14"
        implementation "androidx.compose.material3:material3:$vMaterial3"

        // Debug
        debugImplementation "androidx.compose.ui:ui-tooling:$v"
    }

    dependencies /* Androidx.concurrent */ {

        // Futures
        def vFutures = "1.1.0"
        implementation "androidx.concurrent:concurrent-futures:$vFutures"
    }

    dependencies /* Androidx.core */ {
        def vKTX = "1.8.0"
        def vSplashScreen = "1.0.0-rc01"

        implementation "androidx.core:core-ktx:$vKTX"
        implementation "androidx.core:core-splashscreen:$vSplashScreen"
    }

    dependencies /* Androidx.datastore */ {
        def v = "1.0.0"

        implementation "androidx.datastore:datastore:$v"
    }

    dependencies /* Androidx.hilt */ {
        def v = "1.0.0"
        implementation "androidx.hilt:hilt-navigation-compose:$v"
    }

    dependencies /* Androidx.lifecycle */ {
        def v = "2.6.0-alpha01"
        implementation "androidx.lifecycle:lifecycle-runtime-ktx:$v"
    }

    dependencies /* Androidx.media3 */ {
        def v = "1.0.0-beta01"
        implementation "androidx.media3:media3-exoplayer:$v"
        implementation "androidx.media3:media3-session:$v"
        implementation "androidx.media3:media3-common:$v"
        implementation "androidx.media3:media3-ui:$v"
    }

    dependencies /* Androidx.navigation */ {

        // Compose
        def vCompose = "2.5.0"
        implementation "androidx.navigation:navigation-compose:$vCompose"
    }

    dependencies /* Androidx.Room */ {
        def v = "2.4.2"

        implementation "androidx.room:room-runtime:$v"
        implementation "androidx.room:room-ktx:$v"
        kapt "androidx.room:room-compiler:$v"
    }

    dependencies /* Androidx.startup */ {
        def v = "1.1.1"
        implementation "androidx.startup:startup-runtime:$v"
    }

    dependencies /* coil-kt */ {
        def v = "1.4.0"
        implementation "io.coil-kt:coil:$v"
        implementation "io.coil-kt:coil-compose:$v"
    }

    dependencies /* Github.Adonai */ {

        // JAudioTagger
        def vJAudioTagger = "2.3.15"
        implementation "com.github.Adonai:jaudiotagger:$vJAudioTagger"
    }

    dependencies /* Google.accompanist */ {
        def v = "0.24.3-alpha"
        implementation "com.google.accompanist:accompanist-drawablepainter:$v"
        implementation "com.google.accompanist:accompanist-navigation-material:$v"
        implementation "com.google.accompanist:accompanist-permissions:$v"
        implementation "com.google.accompanist:accompanist-placeholder:$v"
        implementation "com.google.accompanist:accompanist-systemuicontroller:$v"

        // Pager
        def vPager = "0.24.4-alpha"
        implementation "com.google.accompanist:accompanist-pager:$vPager"

        // Navigation-Animation
        def vNavAnimation = "0.24.4-alpha"
        implementation "com.google.accompanist:accompanist-navigation-animation:$vNavAnimation"
    }

    dependencies /* Google.android */ {

        // Exoplayer
        def vExoPlayer = "2.18.0"
        implementation "com.google.android.exoplayer:extension-mediasession:$vExoPlayer"
    }

    dependencies /* Google.dagger */ {

        // Hilt-Android
        def vHiltAndroid = "2.41"
        implementation "com.google.dagger:hilt-android:$vHiltAndroid"
        kapt "com.google.dagger:hilt-android-compiler:$vHiltAndroid"
    }

    dependencies /* Google.guava */ {
        def v = "31.0.1-android"
        implementation "com.google.guava:guava:$v"
    }

    dependencies /* Instrumentation Test */ {

        // JUnit
        def vJUnitExt = "1.1.3"
        def vJUnit4Compose = "1.1.1"
        androidTestImplementation "androidx.test.ext:junit:$vJUnitExt"
        androidTestImplementation "androidx.compose.ui:ui-test-junit4:$vJUnitExt"

        // Espresso
        def vEspressoCore = "3.4.0"
        androidTestImplementation "androidx.test.espresso:espresso-core:$vEspressoCore"
    }

    dependencies /* JakeWharton.timber */ {
        def v = "5.0.1"
        implementation "com.jakewharton.timber:timber:$v"
    }

    dependencies /* Jetbrains.kotlin */ {

        // reflect
        def vReflect = "1.7.10"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$vReflect"
    }

    dependencies /* Jetbrains.kotlinx */ {

        // Coroutines-Guava
        def vCoroutines_Guava = "1.6.1"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-guava:$vCoroutines_Guava"

        // Serialization-Json
        def vSerialization_Json = "1.3.2"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$vSerialization_Json"
    }

    dependencies /* SquareUp.leakcanary */ {
        def v = "2.9.1"
        debugImplementation "com.squareup.leakcanary:leakcanary-android:$v"
    }

    dependencies /* Unit Test */ {
        def v = "4.13.2"

        // JUnit
        testImplementation "junit:junit:$v"
    }

    dependencies /* wasabeef.transformers*/ {
        // Coil
        /* 1.0.6 Missing transition property on [coil.request.*RequestOptions],
        called from [coil.compose.ImagePainterKt.updatePainter()] */
        def vCoil = "1.0.5"
        implementation "jp.wasabeef.transformers:coil:$vCoil"
        implementation "jp.wasabeef.transformers:coil-gpu:$vCoil"
    }
}